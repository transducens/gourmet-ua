from os.path import join

#Output dirs
permanentDir = config["permanentDir"]
corpusDir = permanentDir + "/corpus"
evalDir = permanentDir + "/eval"
modelDir = permanentDir + "/model"

#Software dependencies
marian=config["marianDir"]
moses=config["mosesDir"]
subword_nmt=config["subwordNmtDir"]
bpeOperations=config["bpeOperations"]
tools=config["toolsDir"]
smartSegmentation=config["smartSegmentationDir"]

#Multisource
ssuffix=None
if "ssuffix" in config:
    ssuffix=config["ssuffix"]
wwMark="gourmetwwtranslation"
if "wwMark" in config:
    wwMark=config["wwMark"]
swapInputs=False
if "swapInputs" in config:
    if config["swapInputs"]:
        swapInputs=True

#Interleaving
interleaving_tl=False
analyzer_tl=None
if "analyzer_tl" in config:
    interleaving_tl=True
    analyzer_tl=config["analyzer_tl"]

LANG1=config["lang1"]
LANG2=config["lang2"]

multilangs=[LANG1,LANG2]
multilangs_test=[LANG1]
if ssuffix:
    if swapInputs:
        multilangs=[LANG2+".2s",LANG1,LANG2]
        multilangs_test=[LANG2+".2s",LANG1]
    else:
        multilangs=[LANG1,LANG2+".2s",LANG2]
        multilangs_test=[LANG1,LANG2+".2s"]

#Precomputed BPE, vocab, truecasers
externalVocab=""
if "externalVocab" in config:
    externalVocab=config["externalVocab"]
externalBpe=""
if "bpeModel" in config:
    externalBpe=config["bpeModel"]

externalTruecasers={LANG1:"", LANG2:""}
if "externalTruecasers" in config:
    externalTruecasers=config["externalTruecasers"]

#Mark for multilingual MT
addMark={LANG1:"", LANG2:"", LANG2+"."+str(ssuffix):""}
if "addMark" in config:
    addMark=config["addMark"]

#Morphological segmentation
segModel={LANG1:"", LANG2:""}
if "segModel" in config:
    segModel=config["segModel"]

segParams=""
if "segParams" in config:
    segParams=config["segParams"]

tokOptions="-a"
if interleaving_tl:
    tokOptions="-a -no-escape"
tokenizer_l1 = moses + "/scripts/tokenizer/normalize-punctuation.perl -l " + LANG1 + " | " + moses + "/scripts/tokenizer/tokenizer.perl "+tokOptions+" -l " + LANG1
tokenizer_l2 = moses + "/scripts/tokenizer/normalize-punctuation.perl -l " + LANG2 + " | " + moses + "/scripts/tokenizer/tokenizer.perl "+tokOptions+" -l " + LANG2
detokenizer = moses + "/scripts/tokenizer/detokenizer.perl -l " + LANG2

sentenceSplitter1 = moses + "/scripts/ems/support/split-sentences.perl -b -l " + LANG1
sentenceSplitter2 = moses + "/scripts/ems/support/split-sentences.perl -b -l " + LANG2

#NMT commands
trainCmd = "{0}/build/marian -d {1} ".format(marian, config["gpuId"]) + " ".join(config["marianArgs"]) + " --valid-log " + permanentDir +"/valid.log --log " + permanentDir + "/train.log"

translateCmd = "{0}/build/marian-decoder --max-length-crop -d {1} --quiet-translation --max-length 100".format(marian, config["gpuId"])

bleuCmd = moses + "/scripts/generic/multi-bleu-detok.perl"

#Input data prefixes
trainPrefixes=config["initCorpusTrainPrefix"]
devPrefix=config["initCorpusDevPrefix"]
testPrefixes=config["initCorpusTestPrefix"]
assert(len(devPrefix) == 1)

############################################# EVALUATION #############################################################

def allTestNames(dataset):
    names = []
    for f in dataset:
        names.append(os.path.basename(f))
    return names

rule report:
    input:
        expand(evalDir+"/{name}.bleu", name=allTestNames(testPrefixes))
    output:
        evalDir+"/report"
    run:
        with open(output[0], "wt") as outHandle:
            for file in input:
                with open(file, "rt") as inHandle:
                    str = inHandle.read()
                    outHandle.write(os.path.basename(file).replace(".bleu",""))
                    outHandle.write("\t")
                    outHandle.write(str)

rule multibleu:
    input:
        trans=evalDir+"/{name}.output.detokenized"
        ,
        ref=corpusDir+"/{name}."+"{lang}".format(lang=LANG2)
    output:
        evalDir+"/{name}.bleu"
    shell:
        "cat {input.trans} | {bleuCmd} {input.ref} > {output}"

rule translate_only:
    input:
        trans=evalDir+"/in.output.detokenized"

    output:
        evalDir+"/output"
    shell:
      	#"hostname;"
        "cp {input} {output}"

##########################################  RUNNING MT ENGINE ##########################################


rule translate_test:
    input:
        modelPrevious="{dir}/marian/model.npz.decoder.yml".format(dir=modelDir)
        ,
        test= expand("{corpusDir}/{{name}}.bpe.{lang}",corpusDir=corpusDir,lang=multilangs_test)
    output:
        evalDir+"/{name}.output"
    params:
        model="{dir}/marian/{modelFile}".format(dir=modelDir, modelFile=config["marianModelFile"])

    shell:
        "{translateCmd} -i {input.test} -c {params.model} > {output}"

rule train_nmt:
    input:
        vocab=modelDir+"/vocab."+LANG1+LANG2+".yml",
        train=expand("{corpusDir}/train.clean-bpe.{lang}",corpusDir=corpusDir,lang=multilangs),
        valid=expand("{corpusDir}/dev.bpe.{lang}",corpusDir=corpusDir,lang=multilangs)
    output:
        "{dir}/marian/model.npz.decoder.yml".format(dir=modelDir)
    params:
        additionalVocab= lambda wildcards,input:  input.vocab if ssuffix else ""
    shell:
        "mkdir -p {modelDir}/marian ;{trainCmd} -t {input.train} --valid-sets {input.valid} --vocabs {input.vocab} {input.vocab} {params.additionalVocab} -m {modelDir}/marian/model.npz"

################################################## MARIAN VOCAB ################################################################

ruleorder: make_vocab_yml_from_external > make_vocab_yml

rule make_vocab_yml:
    input:
        expand("{corpusDir}/train.clean-bpe.{lang}",corpusDir=corpusDir,lang=multilangs)
    output:
        modelDir+"/vocab.{lang1}{lang2}.yml".format(lang1=LANG1,lang2=LANG2)
    shell:
        "cat {input} | {marian}/build/marian-vocab  > {output}"

rule make_vocab_yml_from_external:
    input:
        externalVocab
    output:
        modelDir+"/vocab.{lang1}{lang2}.yml".format(lang1=LANG1,lang2=LANG2)
    shell:
        "cat {input} > {output}"

####################################################### TRUECASE ###########################################################

ruleorder: apply_truecaser_2s > apply_truecaser

rule apply_truecaser:
    input:
        file='{name}.tok.{lang}'
        ,
        model="{dir}/truecaser/".format(dir=modelDir)+"truecase-model.{lang}"
    output:
        '{name}.tc.{lang}'
    shell:
        "cat {input.file} | {moses}/scripts/recaser/truecase.perl -model {input.model} > {output}"

rule apply_truecaser_2s:
    input:
        file='{name}.tok.{lang}.2s'
        ,
        model="{dir}/truecaser/".format(dir=modelDir)+"truecase-model.{lang}"
    output:
        '{name}.tc.{lang}.2s'
    shell:
        "cat {input.file} | {moses}/scripts/recaser/truecase.perl -model {input.model} > {output}"

ruleorder: apply_truecaser_train_2s > apply_truecaser_train

rule apply_truecaser_train:
    input:
        file='{name}.clean.{lang}'
        ,
        model="{dir}/truecaser/".format(dir=modelDir)+"truecase-model.{lang}"
    output:
        '{name}.clean-tc.{lang}'
    shell:
        "cat {input.file} | {moses}/scripts/recaser/truecase.perl -model {input.model} > {output}"

rule apply_truecaser_train_2s:
    input:
        file='{name}.clean.{lang}.2s'
        ,
        model="{dir}/truecaser/".format(dir=modelDir)+"truecase-model.{lang}"
    output:
        '{name}.clean-tc.{lang}.2s'
    shell:
        "cat {input.file} | {moses}/scripts/recaser/truecase.perl -model {input.model} > {output}"

ruleorder: learn_truecaser_external > learn_truecaser

rule learn_truecaser:
    input:
        corpusDir+"/train.clean.{lang}"
    output:
        "{dir}/truecaser/".format(dir=modelDir)+"truecase-model.{lang}"
    shell:
        "mkdir -p {modelDir}/truecaser;"
        "{moses}/scripts/recaser/train-truecaser.perl -corpus {input} -model {output}"

rule learn_truecaser_external:
    input:
        l1=externalTruecasers[LANG1],
        l2=externalTruecasers[LANG2]
    output:
        l1=expand("{dir}/truecaser/truecase-model.{lang}",dir=modelDir,lang=LANG1),
        l2=expand("{dir}/truecaser/truecase-model.{lang}",dir=modelDir,lang=LANG2)
    shell:
        "cp {input.l1} {output.l1}; cp {input.l2} {output.l2}"

####################################################### CLEAN ###########################################################

rule clean:
    input:
        "{pref}.tok."+"{lang1}".format(lang1=LANG1)
        ,
        "{pref}.tok."+"{lang2}".format(lang2=LANG2)
    output:
        "{pref}.clean."+"{lang1}".format(lang1=LANG1)
        ,
        "{pref}.clean."+"{lang2}".format(lang2=LANG2),
        "{pref}.lines-retained"
    shell:
        "{moses}/scripts/training/clean-corpus-n.perl {wildcards.pref}.tok {LANG1} {LANG2} {wildcards.pref}.clean 1 80 {wildcards.pref}.lines-retained"

rule clean_2s:
    input:
        tok="{pref}.tok.{lang}.2s"
        ,
        retained="{pref}.lines-retained"
    output:
        "{pref}.clean.{lang}.2s"
    shell:
        "python3 {tools}/filter-lines-retained.py {input.retained} < {input.tok}  >{output}"

####################################################### TOKENIZE ###########################################################

rule tokenize_file_l1:
    input:
        "{pref}."+"{lang}".format(lang=LANG1)
    output:
        "{pref}.tok."+"{lang}".format(lang=LANG1)
    shell:
        "cat {input} | {tokenizer_l1} > {output}"

rule tokenize_file_l2:
    input:
        "{pref}."+"{lang}".format(lang=LANG2)
    output:
        "{pref}.tok."+"{lang}".format(lang=LANG2)
    shell:
        "cat {input} | {tokenizer_l2} > {output}"

rule tokenize_file_l2_2s:
    input:
        "{pref}."+"{lang}.2s".format(lang=LANG2)
    output:
        "{pref}.tok."+"{lang}.2s".format(lang=LANG2)
    shell:
        "cat {input} | {tokenizer_l2} > {output}"


####################################################### POSTPROCESSING ###########################################################

rule detok:
    input:
        "{pref}.output.detruecased"
    output:
        "{pref}.output.detokenized"
    shell:
        "cat {input} | {detokenizer} > {output}"

rule detruecase:
    input:
        "{pref}.output.debpe"
    output:
        "{pref}.output.detruecased"
    shell:
        "cat {input} | {moses}/scripts/recaser/detruecase.perl > {output}"

rule debpe:
    input:
        "{pref}.output"
    output:
        "{pref}.output.debpe"
    shell:
        "cat {input} | sed -r 's/(@@ )|(@@ ?$)//g' > {output}"

##################################### MORPHOLOGICAL SEGMENTATION ##################

ruleorder: morph_segment_2s > morph_segment

rule morph_segment:
    input:
        '{name}tc.{lang}'
    output:
        '{name}tc.seg.{lang}'
    params:
        sModel=lambda w: segModel[w.lang]
    shell:
        "if [ '{params.sModel}' != ''  ]; then bash {smartSegmentation}/segment.sh -c {input} -m {params.sModel} -l {wildcards.lang} {segParams}  > {output} ; else cp {input} {output}  ;  fi"

rule morph_segment_2s:
    input:
        '{name}tc.{lang}.2s'
    output:
        '{name}tc.seg.{lang}.2s'
    params:
        sModel=lambda w: segModel[w.lang]
    shell:
        "if [ '{params.sModel}' != ''  ]; then bash {smartSegmentation}/segment.sh -c {input} -m {params.sModel} -l {wildcards.lang} {segParams}  > {output} ; else cp {input} {output}  ;  fi"


############################################## BPE ##############################################

rule apply_bpe_test:
    input:
        file="{pref}.tc.seg.{lang}"
        ,
        vocab="{dir}/".format(dir=modelDir)+"vocab.{lang1}{lang2}.bpe".format(lang1=LANG1, lang2=LANG2)
    output:
        "{pref}.bpe.{lang}"
    params:
        mark=lambda w: addMark[w.lang]
    shell:
        "{subword_nmt}/subword_nmt/apply_bpe.py --glossaries {wwMark}  -c {input.vocab} < {input.file} |  sed 's: ￭:@@ :g' |  {{ if [ \"{params.mark}\" != \"\" ]; then  sed 's:^:{params.mark} :' ; else cat -;  fi  }} > {output}"

rule apply_bpe_train:
    input:
        file="{pref}.clean-tc.seg.{lang}"
        ,
        vocab="{dir}/".format(dir=modelDir)+"vocab.{lang1}{lang2}.bpe".format(lang1=LANG1, lang2=LANG2)
    output:
        "{pref}.clean-bpe.{lang}"
    params:
        mark=lambda w: addMark[w.lang]
    shell:
        "if [ \"{interleaving_tl}\" == \"True\" -a \"{wildcards.lang}\" == \"{LANG2}\" ]; then  {analyzer_tl} < {wildcards.pref}.tok.{wildcards.lang} > {wildcards.pref}.tok.{wildcards.lang}.analyzed ; fi; {subword_nmt}/subword_nmt/apply_bpe.py  --glossaries {wwMark} -c {input.vocab} < {input.file} | sed 's: ￭:@@ :g' |  {{ if [ \"{params.mark}\" != \"\" ]; then  sed 's:^:{params.mark} :' ; else cat -;  fi  }} | {{ if [ \"{interleaving_tl}\" == \"True\" -a \"{wildcards.lang}\" == \"{LANG2}\" ]; then python3 {toolsDir}/scripts/generate-interleaving-corpus.py  {wildcards.pref}.tok.{wildcards.lang}.analyzed  ; else cat - ; fi  }} > {output}"

ruleorder: learn_bpe_external > learn_bpe

rule learn_bpe:
    input:
        expand("{corpusDir}/train.clean-tc.seg.{lang}",corpusDir=corpusDir,lang=multilangs)
    output:
        modelDir+"/vocab."+"{lang1}{lang2}.bpe".format(lang1=LANG1, lang2=LANG2 )
    shell:
        "cat {input}|  sed 's:{wwMark}::g'  | sed -r 's:[ ]+: :g' | {subword_nmt}/subword_nmt/learn_bpe.py -s {bpeOperations}  > {output}"

rule learn_bpe_external:
    input:
        externalBpe
    output:
        modelDir+"/vocab."+"{lang1}{lang2}.bpe".format(lang1=LANG1, lang2=LANG2 )
    shell:
        "cat {input} > {output}"

###################################### PREPARE DATA ##############################################

rule prepare_traindata:
    input:
         l1=expand("{dataset}.{lang}", dataset=trainPrefixes, lang=LANG1)
         ,
         l2=expand("{dataset}.{lang}", dataset=trainPrefixes, lang=LANG2)
    output:
         l1=corpusDir+"/train.{lang}".format(lang=LANG1)
         ,
         l2=corpusDir+"/train.{lang}".format(lang=LANG2)
    shell:
         "mkdir -p {corpusDir}; cat {input.l1} > {output.l1} && cat {input.l2} > {output.l2}"

rule prepare_traindata_2s:
    input:
         l2=expand("{dataset}.{lang}.{suf}", dataset=trainPrefixes, lang=LANG2,suf=ssuffix)
    output:
         l2=corpusDir+"/train.{lang}.2s".format(lang=LANG2)
    shell:
         "mkdir -p {corpusDir}; cat {input.l2} > {output.l2}"

rule prepare_devdata:
    input:
         l1=expand("{dataset}.{lang}", dataset=devPrefix, lang=LANG1)
         ,
         l2=expand("{dataset}.{lang}", dataset=devPrefix, lang=LANG2)
    output:
         l1=corpusDir+"/dev.{lang}".format(lang=LANG1)
         ,
         l2=corpusDir+"/dev.{lang}".format(lang=LANG2)
    shell:
         "mkdir -p {corpusDir}; cat {input.l1} > {output.l1} && cat {input.l2} > {output.l2}"

rule prepare_devdata_2s:
    input:
         l2=expand("{dataset}.{lang}.{suf}", dataset=devPrefix, lang=LANG2,suf=ssuffix)
    output:
         l2=corpusDir+"/dev.{lang}.2s".format(lang=LANG2)
    shell:
         "mkdir -p {corpusDir}; cat {input.l2} > {output.l2}"


rule prepare_test:
    input:
         l1=expand("{dataset}.{lang}", dataset=testPrefixes, lang=LANG1)
         ,
         l2=expand("{dataset}.{lang}", dataset=testPrefixes, lang=LANG2)
    output:
         expand(corpusDir+"/{name}.{lang}", name=allTestNames(testPrefixes), lang=LANG1)
         ,
         expand(corpusDir+"/{name}.{lang}", name=allTestNames(testPrefixes), lang=LANG2)
    shell:
         "mkdir -p {corpusDir}; cp -r {input.l1} {input.l2} {corpusDir}"

rule prepare_test_2s:
    input:
         l2=expand("{dataset}.{lang}.{suf}", dataset=testPrefixes, lang=LANG2,suf=ssuffix)
    output:
         expand(corpusDir+"/{name}.{lang}", name=allTestNames(testPrefixes), lang=LANG2+".2s")
    shell:
         "mkdir -p {corpusDir}; cp -r {input.l2} {corpusDir}"
